load(
    ":sources.bzl",
    "crypto_headers",
    "crypto_internal_headers",
    "crypto_sources",
    "crypto_sources_asm",
    "crypto_sources_nasm",
    "bcm_sources",
    "bcm_sources_asm",
    "bcm_sources_nasm",
    "bcm_internal_headers",
    "ssl_headers",
    "ssl_internal_headers",
    "ssl_sources",
    "bssl_internal_headers",
    "bssl_sources",
)

GIT_COMMIT = "baaf868e6e8fa45396070cd8d32d196c404d9ff3"

http_archive(
    name = 'src',
    sha256 = '43f7bdd5848801a2a304917e5173636e727fed49252f7bc28aa926ea207327db',
    urls = [
        'https://github.com/google/boringssl/archive/{}.tar.gz'.format(GIT_COMMIT),
    ],
    type = 'tar.gz',
    strip_prefix = 'boringssl-{}'.format(GIT_COMMIT),
    sub_targets =
        crypto_headers + \
        crypto_internal_headers + \
        crypto_sources + \
        crypto_sources_asm + \
        crypto_sources_nasm + \
        bcm_sources + \
        bcm_sources_asm + \
        bcm_sources_nasm + \
        bcm_internal_headers + \
        ssl_headers + \
        ssl_internal_headers + \
        ssl_sources + \
        bssl_internal_headers + \
        bssl_sources + [
            # rust sources
            'rust/bssl-sys/src/lib.rs',
            'rust/bssl-sys/rust_wrapper.c',
            'rust/bssl-sys/rust_wrapper.h',
            'rust/bssl-sys/wrapper.h',
        ],
    visibility = ['third-party//bssl/...'],
)

src_ref = lambda x: ':src[{}]'.format(x)

cc_copts = [
    # Assembler option --noexecstack adds .note.GNU-stack to each object to
    # ensure that binaries can be built with non-executable stack.
    "-Wa,--noexecstack",
]
cc_copts_cxx = [
    "-std=c++26",
    "-stdlib=libc++",
    "-Wmissing-declarations",
]

boringssl_copts = [
    "-DBORINGSSL_IMPLEMENTATION",
] + cc_copts + select({
    # This is needed on glibc systems to get rwlock in pthreads, but it should
    # not be set on Apple platforms or FreeBSD, where it instead disables APIs
    # we use.
    # See compat(5), sys/cdefs.h, and https://crbug.com/boringssl/471
    "config//os:linux": ["-D_XOPEN_SOURCE=700"],
    # Without WIN32_LEAN_AND_MEAN, <windows.h> pulls in wincrypt.h, which
    # conflicts with our <openssl/x509.h>.
    "config//os:windows": ["-DWIN32_LEAN_AND_MEAN", "-DNOMINMAX"],
    "DEFAULT": [],
}) + select({
    "config//os:windows": ["-DOPENSSL_NO_ASM"],
    "DEFAULT": [],
})

boringssl_copts_cxx = boringssl_copts + cc_copts_cxx

cxx_library(
    name = "crypto",
    srcs = map(src_ref, crypto_sources) + select({
        "config//os:windows": map(src_ref, crypto_sources_nasm),
        "DEFAULT": map(src_ref, crypto_sources_asm),
    }) + map(src_ref, bcm_sources) + select({
        "config//os:windows": map(src_ref, bcm_sources_nasm),
        "DEFAULT": map(src_ref, bcm_sources_asm),
    }),
    headers = map(src_ref, crypto_internal_headers)
        + map(src_ref, bcm_internal_headers),
    compiler_flags = boringssl_copts_cxx,

    header_namespace = "",
    exported_headers = {
        x: y for (x, y) in map(lambda x: (x[8:], src_ref(x)), crypto_headers)
    },

    preferred_linkage = "static",
    exported_linker_flags = ["-lc++", "-lc++abi"],
    visibility = ["PUBLIC"],
)

cxx_library(
    name = "ssl",
    srcs = map(src_ref, ssl_sources),
    compiler_flags = boringssl_copts_cxx,

    header_namespace = "",
    exported_headers = {
        x: y for (x, y) in map(lambda x: (x[8:], src_ref(x)), ssl_headers)
    },

    preferred_linkage = "static",
    exported_linker_flags = ["-lc++", "-lc++abi"],
    deps = [ ":crypto" ],
    visibility = ["PUBLIC"],
)

cxx_binary(
    name = "bssl",
    srcs = map(src_ref, bssl_sources),
    compiler_flags = boringssl_copts_cxx,
    deps = [ ":crypto", ":ssl" ],
    visibility = ['PUBLIC'],
)
